dnl Process this file with autoconf to produce a configure script.
AC_INIT
AC_CONFIG_SRCDIR([libaudiofile/aiff.c])

AC_CONFIG_AUX_DIR([../../config])

PACKAGE=audiofile
AUDIOFILE_MAJOR_VERSION=0
AUDIOFILE_MINOR_VERSION=2
AUDIOFILE_MICRO_VERSION=0
AUDIOFILE_VERSION=${AUDIOFILE_MAJOR_VERSION}.${AUDIOFILE_MINOR_VERSION}.${AUDIOFILE_MICRO_VERSION}

dnl set version info for libaudiofile.so to package rev - $MAJOR + $MINOR : $MICRO : $MINOR
AUDIOFILE_VERSION_INFO=2:0:2

AC_SUBST(AUDIOFILE_MAJOR_VERSION)
AC_SUBST(AUDIOFILE_MINOR_VERSION)
AC_SUBST(AUDIOFILE_MICRO_VERSION)
AC_SUBST(AUDIOFILE_VERSION)
AC_SUBST(AUDIOFILE_VERSION_INFO)

VERSION=$AUDIOFILE_VERSION

AM_INIT_AUTOMAKE($PACKAGE, $VERSION)
AM_CONFIG_HEADER(config.h)
AC_DISABLE_SHARED
AM_PROG_LIBTOOL

AC_AIX

dnl Checks whether we are building static libraries.
AM_CONDITIONAL(BUILD_STATIC_LIBS, test x$enable_static = xyes)

AC_ARG_ENABLE(debug,
			[  --disable-debug	  Disable Debugging ],
		 	CXXFLAGS="-O2";CFLAGS="-O2"
			)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_MAKE_SET

dnl Checks for libraries.
dnl Check for the ldexp function in the math library.
AC_CHECK_LIB(m, ldexp)

dnl Check for the SGI Audio Library function ALopenport.
AC_CHECK_LIB(audio, ALopenport)

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h unistd.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_BIGENDIAN
AC_TYPE_OFF_T
AC_TYPE_SIZE_T

dnl Workaround the lack of these typedefs
AC_CHECK_TYPE(u_int8_t, unsigned char)
AC_CHECK_TYPE(u_int16_t, unsigned short)
AC_CHECK_TYPE(u_int32_t, unsigned int)

dnl Checks for library functions.
AC_PROG_GCC_TRADITIONAL
AC_FUNC_MEMCMP
AC_CHECK_FUNCS(strdup)

dnl Set up platform specific stuff
platform=none
AC_MSG_CHECKING([for platform specific tests to compile])
case "$host_os" in
  linux*)
    TEST_BIN="linuxtest"
    platform=linux
    ;;
  irix5* | irix6*)
    TEST_BIN="irixread irixtest irixtestloop"
    platform=irix
    ;;
esac
AC_MSG_RESULT($platform)
AC_SUBST(TEST_BIN)

AC_CONFIG_FILES([
	audiofile.spec
	libaudiofile/Makefile 
	libaudiofile/modules/Makefile 
	Makefile 
	audiofile-config])
AC_CONFIG_COMMANDS([default],[[case "$CONFIG_FILES" in *audiofile-config*) chmod +x audiofile-config;; esac]],[[]])
AC_OUTPUT
